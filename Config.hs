
module Config(
    Arch(..), showArch,
    Program(..), Version,
    defaultVersion, source,
    extractVersion
    ) where

import Data.List.Extra
import Development.Shake.FilePath
import Data.Char

data Arch = Arch32 | Arch64

showArch :: Arch -> String
showArch Arch32 = "i386"
showArch Arch64 = "x86_64"

data Program = GHC | Cabal | MSYS | Alex | Happy deriving (Eq,Show,Enum,Bounded)

type Version = String

defaultVersion :: Program -> Version
-- Latest released versions of all
defaultVersion GHC = "7.10.1"
defaultVersion MSYS = "1.0.1"
defaultVersion Cabal = "1.22.2.0"
defaultVersion Alex = "3.1.4"
defaultVersion Happy = "1.19.5"

source :: Arch -> Program -> Version -> String
-- Official GHC release, available in xv and bz2, but the xv one is harder to extract on Windows systems
source arch GHC ver = "https://www.haskell.org/ghc/dist/" ++ ver ++ "/ghc-" ++ ver ++ "-" ++ showArch arch ++ "-unknown-mingw32.tar.bz2"
-- Official Cabal release as a binary snapshot
source _ Cabal ver@"1.22.2.0" =
    "https://s3.amazonaws.com/download.fpcomplete.com/minghc/cabal-install-" ++ ver ++ "-i386-unknown-mingw32.tar.gz"
source _ Cabal ver = "https://www.haskell.org/cabal/release/cabal-install-" ++ ver ++ "/cabal-" ++ ver ++ "-i386-unknown-mingw32.tar.gz"
-- Generated by running the mingw-get-setup script and zipping up the resulting MSYS directory.
-- See: https://github.com/snoyberg/minghc/issues/6#issuecomment-66904686
source _ MSYS ver = "https://s3.amazonaws.com/download.fpcomplete.com/minghc/msys-" ++ ver ++ ".zip"
-- Both Alex and Happy were generated by compiling with GHC 7.8.3 using the $prefix approach.
-- See: https://github.com/fpco/minghc/issues/24#issuecomment-91231733
source _ Alex ver = "https://s3.amazonaws.com/download.fpcomplete.com/minghc/alex-" ++ ver ++ ".zip"
source _ Happy ver = "https://s3.amazonaws.com/download.fpcomplete.com/minghc/happy-" ++ ver ++ ".zip"


-- | Given a filename containing a version-like bit, extract the version
extractVersion :: String -> Version
extractVersion = intercalate "." . takeWhile f . dropWhile (not . f) . wordsBy (`elem` "-.") . takeFileName
    where f = all isDigit
